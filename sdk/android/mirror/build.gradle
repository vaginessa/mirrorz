plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.21'
    id 'org.mozilla.rust-android-gradle.rust-android' version '0.9.3'
    id 'maven-publish'
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply plugin: 'maven-publish'

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'com.ensarsarajcic.kotlinx:serialization-msgpack:0.5.5'
}

android {
    namespace 'com.github.mycrl'
    compileSdk 34

    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdk 29
        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'arm64-v8a'
        }

        consumerProguardFiles 'consumer-rules.pro'
        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_shared'
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

cargo {
    verbose = true
    profile = 'release'
    apiLevel = 29
    rustupChannel = 'stable'
    prebuiltToolchains = true
    pythonCommand = 'python3'
    module = '../native'
    libname = 'mirror_android'
    targets = ['arm64']
    targetDirectory = '../../../target'
    targetIncludes = ['libmirror_android.so']
    exec { spec, toolchain ->
        def absolutePath = project.file("../../../target").absolutePath
        spec.environment("CARGO_TARGET_DIR", "$absolutePath")
    }
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/mycrl/mirror'
            credentials {
                username = System.getenv('GITHUB_USERNAME')
                password = System.getenv('GITHUB_TOKEN')
            }
        }
    }

    publications {
        release(MavenPublication) {
            groupId = 'com.github.mycrl'
            artifactId = 'mirror'
            version = '0.4.1'

            afterEvaluate {
                from components.release
            }
        }
    }
}
